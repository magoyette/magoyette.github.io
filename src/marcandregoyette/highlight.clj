;; ## Code highlight
;;
;; Syntaxic highlight of code blocks.
(ns marcandregoyette.highlight
  (:require [clygments.core :as clygments]
            [clojure.string :as string]
            [net.cgrand.enlive-html :as enlive])
  (:import java.io.StringReader))

(defn- extract-code
  "Extract the highlighted code from the HTML generated by Clygments."
  [highlighted-code]
  (-> highlighted-code
      StringReader.
      enlive/html-resource
      (enlive/select [:pre])
      first
      :content))

(defn- highlight-code
  "Highlight the code contained in a node with Clygments."
  [node]
  (let [code (->> node
                  :content
                  (apply str))
        lang (as-> node n
                  (:attrs n)
                  (:class n)
                  (string/replace-first n #"^language-" "")
                  (keyword n))]
    (assoc node :content (-> code
                             (clygments/highlight lang :html)
                             extract-code))))

(defn highlight-code-blocks
  "Highlight the code blocks (a code element in a pre element) of the
  HTML of a page. The class highlight is added to the code element
  and the ui, segment and code classes are added to the pre element."
  [page]
  (enlive/sniptest page
                   [:pre :code] highlight-code
                   [:pre :code] #(assoc-in % [:attrs :class] "highlight")
                   [:pre] (enlive/wrap :div {:class "ui segment code"})))
