;; ## Code highlight
;;
;; Syntaxic highlight of code blocks.
;;
;; The code of this namespace was adapted from http://cjohansen.no/building-static-sites-in-clojure-with-stasis.
(ns marcandregoyette.highlight
  (:require [clygments.core :refer [highlight]]
            [net.cgrand.enlive-html :as en]))

(defn- extract-code
  "Extract the highlighted code from the HTML generated by Clygments."
  [highlighted-code]
  (-> highlighted-code
      java.io.StringReader.
      en/html-resource
      (en/select [:pre])
      first
      :content))

(defn- highlight-code
  "Highlight the code contained in a node with Clygments."
  [node]
  (let [code (->> node
                  :content
                  (apply str))
        lang (->> node
                  :attrs
                  :class
                  keyword)]
    (assoc node :content (-> code
                             (highlight lang :html)
                             extract-code))))

(defn highlight-code-blocks
  "Highlight the code blocks (a code element in a pre element) of the
  HTML of a page. The class highlight is added to the code element
  and the ui, segment and code classes are added to the pre element."
  [page]
  (en/sniptest page
               [:pre :code] highlight-code
               [:pre :code] #(assoc-in % [:attrs :class] "highlight")
               [:pre] (en/wrap :div {:class "ui segment code"})))
